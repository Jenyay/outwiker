; Script generated by the Inno Setup Script Wizard.
; SEE THE DOCUMENTATION FOR DETAILS ON CREATING INNO SETUP SCRIPT FILES!

#define MyAppName "OutWiker"
#define MyAppVersion "$version"
#define MyAppPublisher "jenyay.net"
#define MyAppURL "https://jenyay.net/Outwiker/English"
#define MyAppExeName "outwiker.exe"

[Setup]
; NOTE: The value of AppId uniquely identifies this application.
; Do not use the same AppId value in installers for other applications.
; (To generate a new GUID, click Tools | Generate GUID inside the IDE.)
AppId={{ED6D2DAD-1DF8-4779-BCA9-F559CCEE8F25}
AppName={#MyAppName}
AppVersion={#MyAppVersion}
AppPublisher={#MyAppPublisher}
AppPublisherURL={#MyAppURL}
AppSupportURL={#MyAppURL}
AppUpdatesURL={#MyAppURL}
DefaultDirName={code:GetDirName}
DefaultGroupName={#MyAppName}
AllowNoIcons=yes
OutputBaseFilename=$resultname
OutputDir=.
Compression=lzma
SolidCompression=yes
LicenseFile=LICENSE.txt
PrivilegesRequired=lowest
PrivilegesRequiredOverridesAllowed=commandline dialog

[Languages]
Name: "english"; MessagesFile: "compiler:Default.isl"
Name: "russian"; MessagesFile: "compiler:Languages\Russian.isl"
Name: "ukrainian"; MessagesFile: "compiler:Languages\Ukrainian.isl"
Name: "german"; MessagesFile: "compiler:Languages\German.isl"
Name: "italian"; MessagesFile: "compiler:Languages\Italian.isl"

[CustomMessages]
MainFiles=Main Files
russian.MainFiles=Необходимые файлы
german.MainFiles=Erforderliche Dateien
ukrainian.MainFiles=Необхідні файли

Plugins=Plug-ins
german.Plugins=Plugins
russian.Plugins=Плагины
ukrainian.Plugins=Додатки

InstallationMode=Installation mode
russian.InstallationMode=Режим установки
german.InstallationMode=Installationsmodus
ukrainian.InstallationMode=Режим установки

InstallPortableVersion=Store all settings in the application folder
russian.InstallPortableVersion=Хранить настройки в папке программы
german.InstallPortableVersion=Speichern von Einstellungen in den Programmordner
ukrainian.InstallPortableVersion=Зберігати налаштування в папці програми

[Components]
Name: "main"; Description: "{cm:MainFiles}"; Types: full custom compact; Flags: fixed
Name: "plugins"; Description: "{cm:Plugins}"; Types: full custom
Name: "plugins\autorenamer"; Description: "Autorenamer"; Types: full custom
Name: "plugins\counter"; Description: "Counter"; Types: full custom
Name: "plugins\datagraph"; Description: "Datagraph"; Types: full custom
Name: "plugins\diagrammer"; Description: "Diagrammer"; Types: full custom
Name: "plugins\export2html"; Description: "Export2HTML"; Types: full custom
Name: "plugins\externaltools"; Description: "ExternalTools"; Types: full custom
Name: "plugins\hackpage"; Description: "HackPage"; Types: full custom
Name: "plugins\htmlformatter"; Description: "HTMLFormatter"; Types: full custom
Name: "plugins\htmlheads"; Description: "HTMLHeads"; Types: full custom
Name: "plugins\lightbox"; Description: "Lightbox"; Types: full custom
Name: "plugins\livejournal"; Description: "Livejournal"; Types: full custom
Name: "plugins\markdown"; Description: "Markdown"; Types: full custom
Name: "plugins\pagetypecolor"; Description: "PageTypeColor"; Types: full custom
Name: "plugins\readingmode"; Description: "ReadingMode"; Types: full custom
Name: "plugins\recenteditedpages"; Description: "RecentEditedPages"; Types: full custom
Name: "plugins\sessions"; Description: "Sessions"; Types: full custom
Name: "plugins\snippets"; Description: "Snippets"; Types: full custom
Name: "plugins\source"; Description: "Source"; Types: full custom
Name: "plugins\spoiler"; Description: "Spoiler"; Types: full custom
Name: "plugins\statistics"; Description: "Statistics"; Types: full custom
Name: "plugins\tableofcontents"; Description: "TableOfContents"; Types: full custom
Name: "plugins\texequation"; Description: "TeXEquation"; Types: full custom
Name: "plugins\thumbgallery"; Description: "ThumbGallery"; Types: full custom
Name: "plugins\webpage"; Description: "WebPage"; Types: full custom

[Tasks]
Name: "desktopicon"; Description: "{cm:CreateDesktopIcon}"; GroupDescription: "{cm:AdditionalIcons}"; Flags: unchecked
Name: "quicklaunchicon"; Description: "{cm:CreateQuickLaunchIcon}"; GroupDescription: "{cm:AdditionalIcons}"; Flags: unchecked; OnlyBelowVersion: 0,6.1
Name: "portable"; Description: "{cm:InstallPortableVersion}"; GroupDescription: "{cm:InstallationMode}"; Flags: unchecked; Check: not IsAdminInstallMode

[Files]
Source: "outwiker_exe\*"; Excludes: "\plugins\*";  DestDir: "{app}"; Flags: ignoreversion recursesubdirs createallsubdirs; Components: main
Source: "outwiker_exe\plugins\autorenamer\*"; DestDir: "{app}\plugins\autorenamer\"; Flags: ignoreversion recursesubdirs createallsubdirs; Components: plugins\autorenamer
Source: "outwiker_exe\plugins\counter\*"; DestDir: "{app}\plugins\counter\"; Flags: ignoreversion recursesubdirs createallsubdirs; Components: plugins\counter
Source: "outwiker_exe\plugins\datagraph\*"; DestDir: "{app}\plugins\datagraph\"; Flags: ignoreversion recursesubdirs createallsubdirs; Components: plugins\datagraph
Source: "outwiker_exe\plugins\diagrammer\*"; DestDir: "{app}\plugins\diagrammer\"; Flags: ignoreversion recursesubdirs createallsubdirs; Components: plugins\diagrammer
Source: "outwiker_exe\plugins\export2html\*"; DestDir: "{app}\plugins\export2html\"; Flags: ignoreversion recursesubdirs createallsubdirs; Components: plugins\export2html
Source: "outwiker_exe\plugins\externaltools\*"; DestDir: "{app}\plugins\externaltools\"; Flags: ignoreversion recursesubdirs createallsubdirs; Components: plugins\externaltools
Source: "outwiker_exe\plugins\hackpage\*"; DestDir: "{app}\plugins\hackpage\"; Flags: ignoreversion recursesubdirs createallsubdirs; Components: plugins\hackpage
Source: "outwiker_exe\plugins\htmlformatter\*"; DestDir: "{app}\plugins\htmlformatter\"; Flags: ignoreversion recursesubdirs createallsubdirs; Components: plugins\htmlformatter
Source: "outwiker_exe\plugins\htmlheads\*"; DestDir: "{app}\plugins\htmlheads\"; Flags: ignoreversion recursesubdirs createallsubdirs; Components: plugins\htmlheads
Source: "outwiker_exe\plugins\lightbox\*"; DestDir: "{app}\plugins\lightbox\"; Flags: ignoreversion recursesubdirs createallsubdirs; Components: plugins\lightbox
Source: "outwiker_exe\plugins\livejournal\*"; DestDir: "{app}\plugins\livejournal\"; Flags: ignoreversion recursesubdirs createallsubdirs; Components: plugins\livejournal
Source: "outwiker_exe\plugins\markdown\*"; DestDir: "{app}\plugins\markdown\"; Flags: ignoreversion recursesubdirs createallsubdirs; Components: plugins\markdown
Source: "outwiker_exe\plugins\pagetypecolor\*"; DestDir: "{app}\plugins\pagetypecolor\"; Flags: ignoreversion recursesubdirs createallsubdirs; Components: plugins\pagetypecolor
Source: "outwiker_exe\plugins\readingmode\*"; DestDir: "{app}\plugins\readingmode\"; Flags: ignoreversion recursesubdirs createallsubdirs; Components: plugins\readingmode
Source: "outwiker_exe\plugins\recenteditedpages\*"; DestDir: "{app}\plugins\recenteditedpages\"; Flags: ignoreversion recursesubdirs createallsubdirs; Components: plugins\recenteditedpages
Source: "outwiker_exe\plugins\sessions\*"; DestDir: "{app}\plugins\sessions\"; Flags: ignoreversion recursesubdirs createallsubdirs; Components: plugins\sessions
Source: "outwiker_exe\plugins\snippets\*"; DestDir: "{app}\plugins\snippets\"; Flags: ignoreversion recursesubdirs createallsubdirs; Components: plugins\snippets
Source: "outwiker_exe\plugins\source\*"; DestDir: "{app}\plugins\source\"; Flags: ignoreversion recursesubdirs createallsubdirs; Components: plugins\source
Source: "outwiker_exe\plugins\spoiler\*"; DestDir: "{app}\plugins\spoiler\"; Flags: ignoreversion recursesubdirs createallsubdirs; Components: plugins\spoiler
Source: "outwiker_exe\plugins\statistics\*"; DestDir: "{app}\plugins\statistics\"; Flags: ignoreversion recursesubdirs createallsubdirs; Components: plugins\statistics
Source: "outwiker_exe\plugins\tableofcontents\*"; DestDir: "{app}\plugins\tableofcontents\"; Flags: ignoreversion recursesubdirs createallsubdirs; Components: plugins\tableofcontents
Source: "outwiker_exe\plugins\texequation\*"; DestDir: "{app}\plugins\texequation\"; Flags: ignoreversion recursesubdirs createallsubdirs; Components: plugins\texequation
Source: "outwiker_exe\plugins\thumbgallery\*"; DestDir: "{app}\plugins\thumbgallery\"; Flags: ignoreversion recursesubdirs createallsubdirs; Components: plugins\thumbgallery
Source: "outwiker_exe\plugins\webpage\*"; DestDir: "{app}\plugins\webpage\"; Flags: ignoreversion recursesubdirs createallsubdirs; Components: plugins\webpage
; NOTE: Don't use "Flags: ignoreversion" on any shared system files

[Dirs]
Name: "{app}\plugins\"

[Icons]
Name: "{group}\{#MyAppName}"; Filename: "{app}\{#MyAppExeName}"
Name: "{group}\{cm:UninstallProgram,{#MyAppName}}"; Filename: "{uninstallexe}"
Name: "{commondesktop}\{#MyAppName}"; Filename: "{app}\{#MyAppExeName}"; Tasks: desktopicon; Check: IsAdminInstallMode
Name: "{userdesktop}\{#MyAppName}"; Filename: "{app}\{#MyAppExeName}"; Tasks: desktopicon; Check: not IsAdminInstallMode
Name: "{userappdata}\Microsoft\Internet Explorer\Quick Launch\{#MyAppName}"; Filename: "{app}\{#MyAppExeName}"; Tasks: quicklaunchicon


[Run]
Filename: "{app}\{#MyAppExeName}"; Description: "{cm:LaunchProgram,{#StringChange(MyAppName, '&', '&&')}}"; Flags: nowait postinstall skipifsilent

[Code]
function GetDirName(Param: string): string;
begin
  if IsAdminInstallMode then
    Result := ExpandConstant('{commonpf}\Outwiker')
  else
    Result := ExpandConstant('{localappdata}\Outwiker');
end;

procedure CurInstallProgressChanged(CurProgress, MaxProgress: Integer);
var
  IniFileName: string;
begin
  if (CurProgress = MaxProgress) and WizardIsTaskSelected('portable') then begin
    // Create the outwiker.ini file for portable install mode
    IniFileName := GetDirName('') + '\outwiker.ini';
	if not FileExists(IniFileName) then begin
      SaveStringToFile(IniFileName, '', True);
	end;
  end;
end;

function GetUninstallerCommand(): String;
var
  UnInstPath: String;
  UnInstPathWOW6432: String;
  UnInstallString: String;
  UnInstallKey: String;
begin
  UnInstPath := ExpandConstant('Software\Microsoft\Windows\CurrentVersion\Uninstall\{#emit SetupSetting("AppId")}_is1');
  UnInstPathWOW6432 := ExpandConstant('Software\WOW6432Node\Microsoft\Windows\CurrentVersion\Uninstall\{#emit SetupSetting("AppId")}_is1');
  UnInstallKey := 'UninstallString';
	
  UnInstallString := '';

  if IsAdminInstallMode then begin
    RegQueryStringValue(HKLM, UnInstPath, UnInstallKey, UnInstallString);
  end
  else begin
    RegQueryStringValue(HKCU, UnInstPath, UnInstallKey, UnInstallString);
  end;
  
  UnInstallString := RemoveQuotes(UnInstallString);
  Result := UnInstallString;
end;

function InitializeSetup(): Boolean;
var
  UninstallerCommand: string;
  ResultCode: Integer;
begin
  UninstallerCommand := GetUninstallerCommand();
  if UninstallerCommand <> '' then begin
    if WizardSilent then begin
      Exec(UninstallerCommand, '/SILENT /SUPPRESSMSGBOXES', '', SW_HIDE, ewWaitUntilTerminated, ResultCode);
	end
	else begin
	  Exec(UninstallerCommand, '', '', SW_SHOW, ewWaitUntilTerminated, ResultCode);
	end;
	Result := (ResultCode = 0);
  end
  else begin
    Result := True;
  end;
end;
